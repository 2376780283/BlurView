apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 34
    }
    namespace 'eightbitlab.com.blurview'
}

configurations {
    javadocDeps
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.1'
    javadocDeps 'androidx.annotation:annotation:1.8.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.3")
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
}

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    setArchiveClassifier('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    setArchiveClassifier('sources')
    from android.sourceSets.main.java.srcDirs
}
